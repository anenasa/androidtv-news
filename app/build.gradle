plugins {
    id 'com.android.application'
    id 'com.chaquo.python'
}

def useApi21 = providers.gradleProperty("useApi21").getOrElse("false") == "true"
def apiVersion = useApi21 ? 21 : 24
def media3Version = useApi21 ? "1.8.0" : "1.9.2"
def api21Suffix = useApi21 ? "api21-" : ""

android {
    compileSdk = 36

    defaultConfig {
        applicationId "io.github.anenasa.news"
        // No need to update minSdkVersion to 23
        // to use usesCleartextTraffic.
        // They are ignored in older versions.
        // https://stackoverflow.com/a/27100238
        minSdkVersion apiVersion
        // Need to set this to 29 so file access works on Android TV 11 and above
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
        versionCode 40100
        versionName "4.1.0"

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "armeabi-v7a", "arm64-v8a"//, "x86_64"
        }
        packagingOptions {
            doNotStrip "**/libqjs.so"
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                output.outputFileName = "androidtv-news-${api21Suffix}${versionName}.apk"
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'io.github.anenasa.news'
    buildFeatures {
        buildConfig true
    }
    lint {
        abortOnError false
    }
}

chaquopy {
    defaultConfig {
        version = "3.11"
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation "androidx.media3:media3-exoplayer:$media3Version"
    implementation "androidx.media3:media3-exoplayer-hls:$media3Version"
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    // https://github.com/jhy/jsoup/issues/2459
    implementation 'org.jsoup:jsoup:1.21.2'
    implementation 'com.github.mendhak:storage-chooser:2.0.4.4b'
    implementation "ch.acra:acra-core:5.13.1"
    compileOnly "com.google.auto.service:auto-service-annotations:1.1.1"
    annotationProcessor "com.google.auto.service:auto-service:1.1.1"
}
